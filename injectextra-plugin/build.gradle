apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'nexus'
apply plugin: 'maven'

buildscript {
  repositories {
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7'
  }
}

repositories {
  mavenCentral()

  //only needed to develop the lib
  mavenLocal()
}

dependencies {

  //compile 'com.android.tools.build:gradle:0.12.2'
  compile 'com.github.stephanenicolas.javassist:javassist-build-plugin-api:1.0.0'
  //needed to create a processor based on afterburner
  compile 'com.github.stephanenicolas.afterburner:afterburner-library:1.0.1'

  //only needed to create a plugin
  compile 'com.github.stephanenicolas.morpheus:morpheus-plugin:1.0.1'
  compile gradleApi()
  compile localGroovy()

  compile project(':injectresource-annotations')


  compile 'org.projectlombok:lombok:1.14.2'
  compile 'com.google.android:android:4.4.2_r2'

  //--TESTS
  testCompile 'junit:junit:4.11'
  testCompile "org.bouncycastle:bcprov-jdk15:$BOUNCYCASTLE_VERSION"

  testCompile("org.robolectric:robolectric:$ROBOLECTRIC_VERSION") {
    exclude module: 'classworlds'
    exclude module: 'maven-artifact'
    exclude module: 'maven-artifact-manager'
    exclude module: 'maven-error-diagnostics'
    exclude module: 'maven-model'
    exclude module: 'maven-plugin-registry'
    exclude module: 'maven-profile'
    exclude module: 'maven-project'
    exclude module: 'maven-settings'
    exclude module: 'nekohtml'
    exclude module: 'plexus-container-default'
    exclude module: 'plexus-interpolation'
    exclude module: 'plexus-utils'
    exclude module: 'wagon-file'
    exclude module: 'wagon-http-lightweight'
    exclude module: 'wagon-http-shared'
    exclude module: 'wagon-provider-api'
    exclude module: 'commons-logging'
    exclude module: 'httpclient'
    exclude group: 'com.google.android'
  }
  testCompile("org.apache.httpcomponents:httpclient:$HTTP_CLIENT_VERSION")
  testCompile("commons-logging:commons-logging:$COMMONS_LOGGING_VERSION")
  testCompile("com.google.android:android-test:$ANDROID_TEST_VERSION")
  testCompile "org.easymock:easymock:$EASY_MOCK_VERSION"
}

modifyPom {
  project {
    name "Injectresource Gradle Plugin"
    description POM_DESCRIPTION
    url POM_URL

    scm {
      url POM_SCM_URL
      connection POM_SCM_CONNECTION
      developerConnection POM_SCM_DEV_CONNECTION
    }

    licenses {
      license {
        name POM_LICENCE_NAME
        url POM_LICENCE_URL
        distribution POM_LICENCE_DIST
      }
    }

    developers {
      developer {
        id POM_DEVELOPER_ID
        name POM_DEVELOPER_NAME
      }
    }
  }
}

//process test resources via aapt. Needs ANDROID_HOME to be set
task('aaptTestResources') {
  def androidHome = System.getenv("ANDROID_HOME")
  def command = "$androidHome/build-tools/19.1.0/aapt "
  String[] args = [
    "p",
    "-v",
    "-f",
    "-I",
    "$androidHome/platforms/android-20/android.jar",
    "-M",
    "${project.file('src/test/java/AndroidManifest.xml')}",
    "-S",
    "${project.file('src/test/resources')}",
    "-m",
    "-J",
    "${project.file('src/test/java/')}"
  ]
  command += args.join(" ")
  def process = command.execute()
  process.waitFor()
  println "stderr: ${process.err.text}"
  println "stdout: ${process.in.text}"
  if (process.exitValue() != 0) {
    throw new GradleScriptException("Impossible to process test resources", null)
  }
}

test.dependsOn aaptTestResources

//as we want to have non test classes in test source tree
//http://stackoverflow.com/a/14947906/693752
tasks.withType(Test) {
  scanForTestClasses = false
  include "**/*Test.class"
}

